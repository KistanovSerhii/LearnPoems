
// Процедура - Создать статистическую карточку автора
//
// Параметры:
//  Форма					 	- Форма  - форма на которую будут добавлены элементы
//	АвтораУИД					- Строка - Уникальный идентификатор ссылки как строка
//  ИмяАвтора				 	- Строка - Имя автора произведения
//  Количество				 	- Число  - сколько произведений в БД с данным автором
//  ВсегоПроизведений 			- Число  - Всего произведений в БД (без пометки удаления)
//
Процедура СоздатьСтатистическуюКарточкуАвтора(Форма, АвтораУИД, ИмяАвтора, Количество, ВсегоПроизведений) Экспорт
	
	СлучайныйИндекс					= Формат(ТекущаяДата(), "ДФ=ss");
	ИмяАвтораБезСпецСимволов 		= ОбщегоНазначенияСервер.ЗаменитьНеВалидныеСимволыВСтроке(ИмяАвтора);
	
	ЦветаСтиляКарточки				= ЦветаСтиля.ЦветФонаГруппировкиОтчета2;	
	ШрифтАвтор						= Новый Шрифт("Verdana", 16, Истина);
	ШрифтКоличество					= Новый Шрифт("Verdana", 14, Ложь);


#Область ДобавлениеКомандыФормыКакИмяАвтора

	ИмяКомандыАвторСПарам 		= СтрШаблон("КомандаАвтор%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ЗаголовокКомандыАвтор		= АвтораУИД;
	
	НоваяКомандаАвтор 			= Форма.Команды.Добавить(ИмяКомандыАвторСПарам);
	НоваяКомандаАвтор.Действие 	= "АвторНажатие"; // Имя процедуры
	НоваяКомандаАвтор.Заголовок = ЗаголовокКомандыАвтор;
	
#КонецОбласти	
	
#Область Реквизиты

	// Массив для новых реквизитов
	ДобавляемыеРеквизиты = Новый Массив;

#Область ДобавлениеРеквизитовФормыИндикатор
		
	// Опишем ревизиты формы
	ИмяРеквизитаИндикатор	= СтрШаблон("РеквизитИндикатор%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ЗаголовокРекИндикатор	= НСтр(
	"ru = ""Индикатор"";
	|en = ""Indicator""
	|");
	
	Реквизит_Индикатор = Новый РеквизитФормы(
	ИмяРеквизитаИндикатор,
	Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный))
	,,
	ЗаголовокРекИндикатор);
		
	// Заполним массив после описания реквизитов формы
	ДобавляемыеРеквизиты.Добавить(Реквизит_Индикатор);

#КонецОбласти

#Область ДобавлениеРеквизитовФормыКоличество

	// Опишем ревизиты формы
	ИмяРеквизитаКоличество	= СтрШаблон("РеквизитКоличество%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов); 
	ЗаголовокРекКоличество	= НСтр(
	"ru = ""Количество"";
	|en = ""Amount""
	|");
	
	Реквизит_Количество = Новый РеквизитФормы(
	ИмяРеквизитаКоличество,
	Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный))
	,,
	ЗаголовокРекКоличество);
		
	// Заполним массив после описания реквизитов формы
	ДобавляемыеРеквизиты.Добавить(Реквизит_Количество);

#КонецОбласти

	// Добавим новые реквизиты в форму
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
#КонецОбласти

#Область ДобавлениеГруппыКарточкаАвтора	
	
	ИмяГруппыФормыКарточкаАвтора	= СтрШаблон("ГруппаФормыКарточка%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ГруппаФормыАвтор 				= Форма.Элементы.Добавить(ИмяГруппыФормыКарточкаАвтора, Тип("ГруппаФормы"), Форма);
	ГруппаФормыАвтор.Вид 			= ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФормыАвтор.Группировка 	= ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаФормыАвтор.Отображение	= ОтображениеОбычнойГруппы.Нет;
	ГруппаФормыАвтор.Доступность 	= Истина;
	ГруппаФормыАвтор.ТолькоПросмотр = Истина;
	
	ГруппаФормыАвтор.ЦветФона		= ЦветаСтиляКарточки;

#КонецОбласти

#Область ДобавлениеЭлементаФормыКомандаАвтор

	НовыйЭлементКомандаАвтор							= Форма.Элементы.Добавить(ИмяКомандыАвторСПарам, Тип("КнопкаФормы"), ГруппаФормыАвтор);
	НовыйЭлементКомандаАвтор.ИмяКоманды 				= ИмяКомандыАвторСПарам;
	НовыйЭлементКомандаАвтор.Заголовок  				= ИмяАвтора;
	
	НовыйЭлементКомандаАвтор.РастягиватьПоГоризонтали 	= Истина;
	НовыйЭлементКомандаАвтор.ОтображениеФигуры			= ОтображениеФигурыКнопки.ПриАктивности;
	НовыйЭлементКомандаАвтор.Шрифт						= ШрифтАвтор;
	НовыйЭлементКомандаАвтор.ЦветРамки					= ЦветаСтиляКарточки;
	
#КонецОбласти

#Область ДобавлениеГруппыИндикация	
	
	ИмяГруппыФормыИндикация				= СтрШаблон("ГруппаФормы%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ГруппаФормыИндикация 				= Форма.Элементы.Добавить(ИмяГруппыФормыИндикация, Тип("ГруппаФормы"), ГруппаФормыАвтор);
	ГруппаФормыИндикация.Вид 			= ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФормыИндикация.Группировка	= ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ГруппаФормыИндикация.Отображение	= ОтображениеОбычнойГруппы.Нет;
	ГруппаФормыИндикация.Доступность 	= Ложь;

#КонецОбласти

#Область ЭлементыГруппыИндикация

#Область ДобавлениеЭлементовФормыИндикатор

	ИмяЭлементаИндикатор 							= СтрШаблон("ЭлементИндикатор%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ЭлементФормыИндикатор 							= Форма.Элементы.Добавить(ИмяЭлементаИндикатор, Тип("ПолеФормы"), ГруппаФормыИндикация);        
	ЭлементФормыИндикатор.Вид 						= ВидПоляФормы.ПолеИндикатора;
	ЭлементФормыИндикатор.ПутьКДанным 				= ИмяРеквизитаИндикатор;
	ЭлементФормыИндикатор.ЦветРамки					= ЦветаСтиляКарточки;
	
	ЭлементФормыИндикатор.ПоложениеЗаголовка 		= ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементФормыИндикатор.ГоризонтальноеПоложение 	= ГоризонтальноеПоложениеЭлемента.Лево;
	ЭлементФормыИндикатор.АвтоМаксимальнаяШирина	= Истина;
	ЭлементФормыИндикатор.РастягиватьПоГоризонтали	= Истина;
	
	ЭлементФормыИндикатор.ОтображатьПроценты		= Истина;
	ЭлементФормыИндикатор.МаксимальноеЗначение		= ВсегоПроизведений;
	
#КонецОбласти

#Область ДобавлениеЭлементовФормыКоличество

	ИмяЭлементаКоличество 							= СтрШаблон("ЭлементКоличество%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ЭлементФормыКоличество 							= Форма.Элементы.Добавить(ИмяЭлементаКоличество, Тип("ПолеФормы"), ГруппаФормыИндикация);        
	ЭлементФормыКоличество.Вид 						= ВидПоляФормы.ПолеНадписи;
	ЭлементФормыКоличество.ПутьКДанным 				= ИмяРеквизитаКоличество; 	
	ЭлементФормыКоличество.ЦветРамки				= ЦветаСтиляКарточки;
	ЭлементФормыКоличество.Ширина					= 4;
	
	ЭлементФормыКоличество.ПоложениеЗаголовка 		= ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементФормыКоличество.ГоризонтальноеПоложение 	= ГоризонтальноеПоложениеЭлемента.Право;
	ЭлементФормыКоличество.АвтоМаксимальнаяШирина	= Ложь;
	ЭлементФормыКоличество.РастягиватьПоГоризонтали	= Ложь;
	
	ЭлементФормыКоличество.Шрифт					= ШрифтКоличество;
	
#КонецОбласти

#КонецОбласти

#Область УстановитьОбработчикСобытияГиперссылки

	//события "АвторВыбран" !!! процедура должна существовать !!!
	//Форма.Элементы[ИмяЭлементаАвтор].УстановитьДействие("Нажатие", "АвторНажатие");
	
#КонецОбласти

#Область ЗаполнениеДанных

	Форма[ИмяРеквизитаИндикатор] 	= Количество;
	Форма[ИмяРеквизитаКоличество] 	= Количество;
	
#КонецОбласти

КонецПроцедуры