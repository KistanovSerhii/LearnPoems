
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	Ссылка	= ПараметрКоманды;	
	#Если МобильноеПриложениеКлиент Тогда
		ВыбранныеФайлы = "file:///storage/emulated/0/Download/" + Строка(Ссылка) + ".xml";
		ОповещениеПослеВыбораФайлаДляЗаписи(ВыбранныеФайлы, Ссылка);
		Возврат;
	#КонецЕсли
	
	Диалог	= новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Фильтр 		= НСтр("ru = 'Документ'; en = 'Document'") + " XML (*.xml)|*.xml";
  	Диалог.Заголовок 	= НСтр("ru = 'Укажите путь и имя для сохранения'; en = 'Specify the path and name to save'");
	
	Оповещение = новый ОписаниеОповещения("ОповещениеПослеВыбораФайлаДляЗаписи", ЭтотОбъект, Ссылка);	 
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПослеВыбораФайлаДляЗаписи(ВыбранныеФайлы, Ссылка) Экспорт
	
	Если ТипЗнч(ВыбранныеФайлы) = Тип("Массив")	Тогда
		
		выбранныйФайл 	= ВыбранныеФайлы[0];		
		ОбъектXDTO 		= ОбкПроизведениеВXDTO(Ссылка);
		
		ЗаписьXML = Новый ЗаписьXML();
		ЗаписьXML.ОткрытьФайл(выбранныйФайл);
		ЗаписьXML.ЗаписатьОбъявлениеXML();
		ЗаписьXML.ЗаписатьБезОбработки(ОбъектXDTO);
		
		ЗаписьXML.Закрыть();
	Иначе
		Возврат;
	КонецЕсли;
	
	ИнфСообщение = НСтр("ru = 'Выгрузка выполнена!'; en = 'Unload done!'");
	Сообщить(ИнфСообщение);
	
КонецПроцедуры 

&НаСервере
Функция ОбкПроизведениеВXDTO(Обк) Экспорт
	
	ЗаписьXML = Новый ЗаписьXML();
    ЗаписьXML.УстановитьСтроку();	
	
	ТипXDTOПроизведение = ФабрикаXDTO.Тип("http://www.main-app-structurev1.org", "CatalogObject.Произведения");
	ПроизведениеXDTO 	= ФабрикаXDTO.Создать(ТипXDTOПроизведение);
	
	ПроизведениеXDTO.DeletionMark 			= Обк.ПометкаУдаления;
	ПроизведениеXDTO.Description 			= Обк.Наименование;
	ПроизведениеXDTO.Дата 					= Обк.Дата;
	ПроизведениеXDTO._Автор 				= Обк.Автор.Наименование;
	ПроизведениеXDTO.Подсказка 				= Обк.Подсказка;
	ПроизведениеXDTO.Коммент 				= Обк.Коммент;
	ПроизведениеXDTO.СодержитКартинку 		= ЗначениеЗаполнено(Обк.Визуализация);
	
	Если ПроизведениеXDTO.СодержитКартинку Тогда
		ТипXDTOКартинка = ФабрикаXDTO.Тип("http://www.main-app-structurev1.org", "CatalogObject.Картинка");
		КартинкаXDTO 	= ФабрикаXDTO.Создать(ТипXDTOКартинка);

		КартинкаXDTO.DeletionMark	= Обк.ПометкаУдаления;
		КартинкаXDTO.Description	= Обк.Визуализация.Наименование;
		КартинкаXDTO.ДатаЗагрузки	= Обк.Визуализация.ДатаЗагрузки;
		КартинкаXDTO.Коммент		= Обк.Визуализация.Коммент;
		
		КартинкаXDTO.Данные = Обк.Визуализация.Данные;
		
		ПроизведениеXDTO._Визуализация = КартинкаXDTO;
	КонецЕсли;
	
	// ТЧ это объектный тип и его надо отдельно создавать, наполнять и присваивать основному объекту!
	ТипТЧТело 	= ФабрикаXDTO.Тип("http://www.main-app-structurev1.org", "CatalogObject.Произведения.Тело");
	ТЧТелоXDTO	= ФабрикаXDTO.Создать(ТипТЧТело);
		
	ТипСтрокаТЧТело = ФабрикаXDTO.Тип("http://www.main-app-structurev1.org", "CatalogObject.Произведения.Тело.Row");
	Для каждого СтрТЧ Из Обк.Тело Цикл	
		СтрокаТЧТелоXDTO = ФабрикаXDTO.Создать(ТипСтрокаТЧТело);
		СтрокаТЧТелоXDTO.СтрокаДанных	= СтрТЧ.СтрокаДанных;
		СтрокаТЧТелоXDTO.Подсказка		= СтрТЧ.Подсказка;
		
		// Наполняем временный объект ТЧ
		ТЧТелоXDTO.Row.Добавить(СтрокаТЧТелоXDTO);
	КонецЦикла;
	
	// Присваиваем временный ТЧ основному объекту!
	ПроизведениеXDTO.ТЧТело = ТЧТелоXDTO;
	
	ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ПроизведениеXDTO);
	
	Возврат ЗаписьXML.Закрыть();

КонецФункции