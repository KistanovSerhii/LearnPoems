
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// ВсегоПроизведений - это максимум
	максПоказательКоличество = 2;
	
    Если НЕ максПоказательКоличество Тогда	
		Возврат;	
	КонецЕсли;

	СоздатьСтатистическуюКарточкуАвтора(ЭтаФорма, "ИмяАвтора1", 1, максПоказательКоличество);

КонецПроцедуры

&НаСервере
Процедура СоздатьСтатистическуюКарточкуАвтора(Форма, ИмяАвтора, Количество, максПоказательКоличество)
	
	СлучайныйИндекс					= Формат(ТекущаяДата(), "ДФ=ss");
	ИмяАвтораБезСпецСимволов 		= ОбщегоНазначенияСервер.ЗаменитьНеВалидныеСимволыВСтроке(ИмяАвтора);
	
	ЦветаСтиляКарточки				= ЦветаСтиля.ЦветФонаГруппировкиОтчета2;
	
#Область Реквизиты

	// Массив для новых реквизитов
	ДобавляемыеРеквизиты = Новый Массив;

#Область ДобавлениеРеквизитовФормыАвтор
		
	// Опишем ревизиты формы
	ИмяРеквизитаАвтор 	= СтрШаблон("РеквизитАвтор%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов); 
	ЗаголовокРекАвтор	= НСтр(
	"ru = ""Автор"";
	|en = ""Author""
	|");
	
	Реквизит_Автор = Новый РеквизитФормы(
	ИмяРеквизитаАвтор,
	Новый ОписаниеТипов("Строка",,, Новый КвалификаторыСтроки(150))
	,,
	ЗаголовокРекАвтор);
		
	// Заполним массив после описания реквизитов формы
	ДобавляемыеРеквизиты.Добавить(Реквизит_Автор);

#КонецОбласти

#Область ДобавлениеРеквизитовФормыИндикатор
		
	// Опишем ревизиты формы
	ИмяРеквизитаИндикатор	= СтрШаблон("РеквизитИндикатор%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ЗаголовокРекИндикатор	= НСтр(
	"ru = ""Индикатор"";
	|en = ""Indicator""
	|");
	
	Реквизит_Индикатор = Новый РеквизитФормы(
	ИмяРеквизитаИндикатор,
	Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный))
	,,
	ЗаголовокРекИндикатор);
		
	// Заполним массив после описания реквизитов формы
	ДобавляемыеРеквизиты.Добавить(Реквизит_Индикатор);

#КонецОбласти

#Область ДобавлениеРеквизитовФормыКоличество

	// Опишем ревизиты формы
	ИмяРеквизитаКоличество	= СтрШаблон("РеквизитКоличество%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов); 
	ЗаголовокРекКоличество	= НСтр(
	"ru = ""Количество"";
	|en = ""Amount""
	|");
	
	Реквизит_Количество = Новый РеквизитФормы(
	ИмяРеквизитаКоличество,
	Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(10, 0, ДопустимыйЗнак.Неотрицательный))
	,,
	ЗаголовокРекКоличество);
		
	// Заполним массив после описания реквизитов формы
	ДобавляемыеРеквизиты.Добавить(Реквизит_Количество);

#КонецОбласти

	// Добавим новые реквизиты в форму
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
#КонецОбласти

#Область ДобавлениеГруппыКарточкаАвтора	
	
	ИмяГруппыФормыКарточкаАвтора	= СтрШаблон("ГруппаФормыКарточка%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ГруппаФормыАвтор 				= Форма.Элементы.Добавить(ИмяГруппыФормыКарточкаАвтора, Тип("ГруппаФормы"), Форма);
	ГруппаФормыАвтор.Вид 			= ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФормыАвтор.Группировка 	= ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаФормыАвтор.Отображение	= ОтображениеОбычнойГруппы.Нет;
	ГруппаФормыАвтор.Доступность 	= Истина;
	ГруппаФормыАвтор.ТолькоПросмотр = Истина;
	
	ГруппаФормыАвтор.ЦветФона		= ЦветаСтиляКарточки;
	

#КонецОбласти

#Область ЭлементыГруппыКарточка

#Область ДобавлениеЭлементовФормыАвтор

	ИмяЭлементаАвтор 			= СтрШаблон("ЭлементАвтор%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	
	// ГруппаФормыАвтор1 - группа в которую будет помещен реквизит
	ЭлементФормыАвтор 				= Форма.Элементы.Добавить(ИмяЭлементаАвтор, Тип("ПолеФормы"), ГруппаФормыАвтор);        
	ЭлементФормыАвтор.Вид 			= ВидПоляФормы.ПолеНадписи;
	ЭлементФормыАвтор.ПутьКДанным 	= ИмяРеквизитаАвтор; //Тип строка, Объект.ИмяРеквизита или имяРеквизита если это рек. формы
	
	ЭлементФормыАвтор.ПоложениеЗаголовка 		= ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементФормыАвтор.ГоризонтальноеПоложение 	= ГоризонтальноеПоложениеЭлемента.Центр;
	ЭлементФормыАвтор.АвтоМаксимальнаяШирина	= Ложь;
	ЭлементФормыАвтор.РастягиватьПоГоризонтали	= Истина;
	ЭлементФормыАвтор.Гиперссылка				= Истина;
	
	//ЭлементФормыАвтор.Шрифт						= Новый Шрифт(Элементы.Реквизит1.Шрифт,,,,,,,117);
	//стиль: Обычный шрифт текста, 14, полужирный;
	
#КонецОбласти

#КонецОбласти

#Область ДобавлениеГруппыИндикация	
	
	ИмяГруппыФормыИндикация				= СтрШаблон("ГруппаФормы%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ГруппаФормыИндикация 				= Форма.Элементы.Добавить(ИмяГруппыФормыИндикация, Тип("ГруппаФормы"), ГруппаФормыАвтор);
	ГруппаФормыИндикация.Вид 			= ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаФормыИндикация.Группировка	= ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	
	ГруппаФормыИндикация.Отображение	= ОтображениеОбычнойГруппы.Нет;
	ГруппаФормыИндикация.Доступность 	= Ложь;

#КонецОбласти

#Область ЭлементыГруппыИндикация

#Область ДобавлениеЭлементовФормыИндикатор

	ИмяЭлементаИндикатор 				= СтрШаблон("ЭлементИндикатор%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ЭлементФормыИндикатор 				= Форма.Элементы.Добавить(ИмяЭлементаИндикатор, Тип("ПолеФормы"), ГруппаФормыИндикация);        
	ЭлементФормыИндикатор.Вид 			= ВидПоляФормы.ПолеИндикатора;
	ЭлементФормыИндикатор.ПутьКДанным 	= ИмяРеквизитаИндикатор;
	ЭлементФормыИндикатор.ЦветРамки		= ЦветаСтиляКарточки;
	
	ЭлементФормыИндикатор.ПоложениеЗаголовка 		= ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементФормыИндикатор.ГоризонтальноеПоложение 	= ГоризонтальноеПоложениеЭлемента.Лево;
	ЭлементФормыИндикатор.АвтоМаксимальнаяШирина	= Истина;
	ЭлементФормыИндикатор.РастягиватьПоГоризонтали	= Истина;
	
	ЭлементФормыИндикатор.ОтображатьПроценты		= Истина;
	ЭлементФормыИндикатор.МаксимальноеЗначение		= максПоказательКоличество;
	
#КонецОбласти

#Область ДобавлениеЭлементовФормыКоличество

	ИмяЭлементаКоличество 				= СтрШаблон("ЭлементКоличество%1_%2", СлучайныйИндекс, ИмяАвтораБезСпецСимволов);
	ЭлементФормыКоличество 				= Форма.Элементы.Добавить(ИмяЭлементаКоличество, Тип("ПолеФормы"), ГруппаФормыИндикация);        
	ЭлементФормыКоличество.Вид 			= ВидПоляФормы.ПолеНадписи;
	ЭлементФормыКоличество.ПутьКДанным 	= ИмяРеквизитаКоличество; 	
	ЭлементФормыКоличество.ЦветРамки	= ЦветаСтиляКарточки;
	ЭлементФормыКоличество.Ширина		= 4;
	
	ЭлементФормыКоличество.ПоложениеЗаголовка 		= ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементФормыКоличество.ГоризонтальноеПоложение 	= ГоризонтальноеПоложениеЭлемента.Право;
	ЭлементФормыКоличество.АвтоМаксимальнаяШирина	= Ложь;
	ЭлементФормыКоличество.РастягиватьПоГоризонтали	= Ложь;
	
#КонецОбласти

#КонецОбласти

#Область УстановитьОбработчикСобытия

	//события "АвторВыбран" !!! процедура должна существовать !!!
	ЭтаФорма.Элементы[ИмяЭлементаАвтор].УстановитьДействие("Нажатие", "АвторНажатие");
	
#КонецОбласти

#Область ЗаполнениеДанных

 	// устанавливаем значение реквизита
	Форма[ИмяРеквизитаАвтор] 		= ИмяАвтора;
	Форма[ИмяРеквизитаИндикатор] 	= Количество;
	Форма[ИмяРеквизитаКоличество] 	= Количество;
	
#КонецОбласти

КонецПроцедуры

&НаКлиенте
Процедура АвторНажатие(Элемент, СтандартнаяОбработка)
	
    Сообщить("Привет!");
	
КонецПроцедуры
